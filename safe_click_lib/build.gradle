plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'kotlin-android'
    id 'org.jetbrains.kotlinx.kover' version "$kover_version"
}

def libraryVersionName = '1.0.4'
def artifactName = 'SafeClick'

android {
    namespace 'vit.khudenko.android.safe_click'

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    compileSdk 33

    buildToolsVersion '33.0.2'

    defaultConfig {
        minSdk 21
        targetSdk 33
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    testOptions {
        unitTests.all {
            if (name == "testReleaseUnitTest") {
                kover {
                    enabled = true
                }
            }
        }
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            // withJavadocJar()
        }
    }
}

tasks.koverHtmlReport {
    enabled = true
    htmlReportDir.set(file("/reports/kover"))
}

tasks.koverXmlReport {
    enabled = false
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.13.5'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                from components.release

                groupId 'com.github.vitkhudenko'
                artifactId 'safe_click'
                version libraryVersionName

                pom {
                    name = artifactName
                    description = 'A solution to redundant click handling for Android development.'
                    url = 'https://github.com/vitkhudenko/safe_click'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/vitkhudenko/safe_click/blob/master/LICENSE'
                        }
                    }
                }

            }
        }
    }
}